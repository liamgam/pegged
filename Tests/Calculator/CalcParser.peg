
@option case-insensitive;
@property (retain) Calculator *calculator;

Equation <- Primary EndOfFile

Primary <- Secondary ( PLUS  Secondary { [self.calculator add]; }
                     / MINUS Secondary { [self.calculator subtract]; }
                     )*

Secondary <- Tertiary ( MUL Tertiary { [self.calculator multiply]; }
                      / DIV Tertiary { [self.calculator divide];   }
                      )*

Tertiary <- Terminal EXP Tertiary { [self.calculator exponent]; }
          / (PLUS / MINUS { [self.calculator negative]; })+ Tertiary
          / Terminal

Terminal <- OPEN Primary CLOSE
          / "pow" OPEN Primary COMMA Primary CLOSE { [self.calculator exponent]; }
          / Number { [self.calculator pushNumber:text]; }

Number <- < [\x30-\x39]+ > _

OPEN      <- '(' _
CLOSE     <- ')' _
COMMA     <- ',' _

EXP       <- '**' _
MUL       <- '*' _
DIV       <- '/' _
PLUS      <- '+' _
MINUS     <- '-' _
_         <- ' '*
EndOfFile <- !.
